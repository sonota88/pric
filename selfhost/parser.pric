# -*- mode: ruby -*-

#include lib/std.pric
#include lib/words.pric
#include lib/types.pric
#include lib/json.pric

def get_line_size(input_, pos_from)
  var pos_to;
  var lf_pos = char_index(input_, C_LF(), pos_from);

  if (lf_pos != -1)
    pos_to = lf_pos + 1;
  else
    # LF not found
    pos_to = char_index(input_, 0, pos_from);
  end

  return __sub(pos_to, pos_from);
end

def set_token(g_, ti, t_)
  if (__le(NUM_TOKENS_MAX(), ti))
    p_int(NUM_TOKENS_MAX());
    p_int(ti);
    panic(27); # set_token: cap over
  end
  aset(g_ + GO_TOKENS(), ti, t_);
end

def read_tokens(g_, input_)
  var do_break = FALSE();
  var line_size = 30; var [30]line;
  var size;
  var pos = 0;
  var sep_i;
  var [6]kind_str;
  var value_str_size = 64; var [64]value_str;
  var t_;
  var ti;
  check_heap_stack_overlap(g_);

  while (__not(do_break))
    size = get_line_size(input_, pos);
    if (size == 0)
      do_break = TRUE();
    else
      substr_with_check(&line, input_, pos, pos + size, line_size);
      chomp(&line);

      sep_i = char_index(&line, 58, 0); # :
      substr(&kind_str, &line, 0, sep_i);
      substr_with_check(&value_str, &line, sep_i + 1, str_size(&line), value_str_size);

      t_ = Token_new(g_, &kind_str, &value_str);
      set_token(g_, ti, t_);
      ti = ti + 1;

      pos = pos + size;
    end
  end

  return ti;
end

def set_num_tokens(g_, num_tokens)
  *(g_ + GO_NUM_TOKENS()) = num_tokens;
end

def get_num_tokens(g_)
  return *(g_ + GO_NUM_TOKENS());
end

def is_end(g_)
  var num_tokens = get_num_tokens(g_);
  var ti = get_token_index(g_);

  return __le(num_tokens, ti);
end

# --------------------------------

def set_token_index(g_, ti)
  *(g_ + GO_TOKEN_INDEX()) = ti;
end

def get_token_index(g_)
  return *(g_ + GO_TOKEN_INDEX());
end

def inc_token_index(g_)
  set_token_index(
    g_,
    get_token_index(g_) + 1
  );
end

def peek(g_, i)
  var ti = get_token_index(g_);
  return *(g_ + GO_TOKENS() + ti + i);
end

def consume_kw(g_, str_)
  var t_ = peek(g_, 0);
  if (__and(
        Token_kind_eq(t_, TOKEN_KIND__KW()),
        Token_val_eq(t_, str_)
  ))
    inc_token_index(g_);
  else
    p_int(Token_get_kind(t_));
    print_s(Token_get_val(t_));
    print_s(str_);
    panic(98); # consume_kw
  end
end

def consume_sym(g_, str_)
  var t_ = peek(g_, 0);
  if (__and(
        Token_kind_eq(t_, TOKEN_KIND__SYM()),
        Token_val_eq(t_, str_)
  ))
    inc_token_index(g_);
  else
    p_str(str_); # expected
    p_int(Token_get_kind(t_));
    p_str(Token_get_val(t_)); # actual
    panic(117); # consume_sym: unexpected token
  end
end

# --------------------------------

def consume_sym_one_char(g_, c)
  var [2]str;

  aset(&str, 0, c);
  aset(&str, 1, 0);

  consume_sym(g_, &str);
end

def consume_sym_paren_l(g_)
  consume_sym_one_char(g_, 40); # (
end

def consume_sym_paren_r(g_)
  consume_sym_one_char(g_, 41); # )
end

def consume_sym_cbracket_l(g_)
  consume_sym_one_char(g_, 123); # {
end

def consume_sym_cbracket_r(g_)
  consume_sym_one_char(g_, 125); # }
end

def consume_sym_semicolon(g_)
  consume_sym_one_char(g_, 59); # ;
end

def consume_sym_equal(g_)
  consume_sym_one_char(g_, 61); # =
end

# --------------------------------

def _parse_arg(g_)
  var n;
  var t_ = peek(g_, 0);
  inc_token_index(g_);

  case
  when (Token_kind_eq(t_, TOKEN_KIND__IDENT()))
    return Node_new_str(g_, Token_get_val(t_));
  when (Token_kind_eq(t_, TOKEN_KIND__INT()))
    n = parse_int(Token_get_val(t_));
    return Node_new_int(g_, n);
  else
    panic(202); # _parse_arg: unsupported
  end
end

def parse_args(g_)
  var args_;
  var t_;
  var arg_node_;
  var do_break = FALSE();
  var [2]s_comma;

  aset(&s_comma, 0, 44); # ,
  aset(&s_comma, 1,  0);

  args_ = NodeList_new(g_);

  t_ = peek(g_, 0);

  if (char_at(Token_get_val(t_), 0) == 41) # )
    return args_;
  else

    arg_node_ = _parse_arg(g_);
    NodeList_add(args_, arg_node_);

    while (__not(do_break))
      t_ = peek(g_, 0);

      if (char_at(Token_get_val(t_), 0) == 44) # ,
        consume_sym(g_, &s_comma);
        arg_node_ = _parse_arg(g_);
        NodeList_add(args_, arg_node_);
      else
        do_break = TRUE();
      end
    end

    return args_;

  end
end

def parse_func(g_)
  var func_stmt_;
  var [10]temp_str;
  var t_;
  var stmts_;
  var stmt_;
  var do_break = FALSE();
  var [4]str_var;
  var [5]str_func;
  var args_;
  var fn_name_;
  check_heap_stack_overlap(g_);

  str_set_var(&str_var);
  str_set_func(&str_func);

  consume_kw(g_, &str_func);

  func_stmt_ = NodeList_new(g_);

  NodeList_add_str(g_, func_stmt_, &str_func);

  t_ = peek(g_, 0);
  inc_token_index(g_);
  fn_name_ = Token_get_val(t_);
  NodeList_add_str(g_, func_stmt_, fn_name_);

  consume_sym_paren_l(g_); # (

  args_ = parse_args(g_);
  NodeList_add_list(g_, func_stmt_, args_);

  consume_sym_paren_r(g_); # )

  consume_sym_cbracket_l(g_); # {

  stmts_ = NodeList_new(g_);

  while(__not(do_break))
    t_ = peek(g_, 0);
    if (char_at(Token_get_val(t_), 0) == 125) # }
      do_break = TRUE();
    else

      if (str_eq(Token_get_val(t_), &str_var))
        stmt_ = parse_var(g_);
      else
        stmt_ = parse_stmt(g_);
      end
      NodeList_add_list(g_, stmts_, stmt_);

    end
  end

  consume_sym_cbracket_r(g_); # }

  NodeList_add_list(g_, func_stmt_, stmts_);

  return func_stmt_;
end

def _parse_var_declare(g_)
  var stmt_;
  var [4]str_var;
  var t_;
  var var_name_;
  check_heap_stack_overlap(g_);

  str_set_var(&str_var);

  stmt_ = NodeList_new(g_);

  consume_kw(g_, &str_var);
  NodeList_add_str(g_, stmt_, &str_var);

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var_name_ = Token_get_val(t_);
  NodeList_add_str(g_, stmt_, var_name_);

  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def _parse_var_init(g_)
  var stmt_;
  var [4]str_var;
  var t_;
  var var_name_;
  var expr_node_;
  check_heap_stack_overlap(g_);

  str_set_var(&str_var);

  stmt_ = NodeList_new(g_);

  consume_kw(g_, &str_var);
  NodeList_add_str(g_, stmt_, &str_var);

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var_name_ = Token_get_val(t_);
  NodeList_add_str(g_, stmt_, var_name_);

  consume_sym_equal(g_); # =

  expr_node_ = parse_expr(g_);
  NodeList_add(stmt_, expr_node_);

  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def parse_var(g_)
  var [2]str_semicolon;
  var [2]str_equal;
  var t_;

  aset(&str_semicolon, 0, 59); #;
  aset(&str_semicolon, 1,  0);

  aset(&str_equal, 0, 61); # =
  aset(&str_equal, 1,  0);

  t_ = peek(g_, 2);
  case
  when (Token_val_eq(t_, &str_semicolon))
    _parse_var_declare(g_);
  when (Token_val_eq(t_, &str_equal))
    _parse_var_init(g_);
  else
    print_s(Token_get_val(t_));
    panic(240); # must not happen
  end

end

def _parse_expr_right(g_, expr_node_l_)
  var t_ = peek(g_, 0);
  var expr_node_r_;
  var expr_els_;
  var [2]str_plus;
  var [2]str_star;
  var [3]str_double_equal;
  var [3]str_not_equal;
  var [3]str_eq_op;
  var [4]str_neq_op;

  aset(&str_plus, 0, 43); # +
  aset(&str_plus, 1,  0);

  aset(&str_star, 0, 42); # *
  aset(&str_star, 1,  0);

  aset(&str_double_equal, 0, 61); # =
  aset(&str_double_equal, 1, 61); # =
  aset(&str_double_equal, 2,  0);

  aset(&str_not_equal, 0, 33); # !
  aset(&str_not_equal, 1, 61); # =
  aset(&str_not_equal, 2,  0);

  aset(&str_eq_op, 0, 101); # e
  aset(&str_eq_op, 1, 113); # q
  aset(&str_eq_op, 2,   0);

  aset(&str_neq_op, 0, 110); # n
  aset(&str_neq_op, 1, 101); # e
  aset(&str_neq_op, 2, 113); # q
  aset(&str_neq_op, 3,   0);

  if (char_at(Token_get_val(t_), 0) == 41) # )
    return expr_node_l_;
  else

    if (char_at(Token_get_val(t_), 0) == 59) #;
      return expr_node_l_;
    else

      case
      when (char_at(Token_get_val(t_), 0) == 43) # +
        consume_sym(g_, &str_plus);
        expr_node_r_ = parse_expr(g_);

        expr_els_ = NodeList_new(g_);

        NodeList_add_str(g_, expr_els_, &str_plus);
        NodeList_add(expr_els_, expr_node_l_);
        NodeList_add(expr_els_, expr_node_r_);

        return Node_new_list(g_, expr_els_);

      when (char_at(Token_get_val(t_), 0) == 42) # *
        consume_sym(g_, &str_star);
        expr_node_r_ = parse_expr(g_);

        expr_els_ = NodeList_new(g_);

        NodeList_add_str(g_, expr_els_, &str_star);
        NodeList_add(expr_els_, expr_node_l_);
        NodeList_add(expr_els_, expr_node_r_);

        return Node_new_list(g_, expr_els_);

      when (Token_val_eq(t_, &str_double_equal))
        consume_sym(g_, &str_double_equal);
        expr_node_r_ = parse_expr(g_);

        expr_els_ = NodeList_new(g_);

        NodeList_add_str(g_, expr_els_, &str_eq_op);
        NodeList_add(expr_els_, expr_node_l_);
        NodeList_add(expr_els_, expr_node_r_);

        return Node_new_list(g_, expr_els_);

      when (Token_val_eq(t_, &str_not_equal))
        consume_sym(g_, &str_not_equal);
        expr_node_r_ = parse_expr(g_);

        expr_els_ = NodeList_new(g_);

        NodeList_add_str(g_, expr_els_, &str_neq_op);
        NodeList_add(expr_els_, expr_node_l_);
        NodeList_add(expr_els_, expr_node_r_);

        return Node_new_list(g_, expr_els_);

      else

        p_str(Token_get_val(t_));
        panic(380); # _parse_expr_right
      end

    end

  end
end

def parse_expr(g_)
  var t_left_;
  var n;
  var expr_node_l_;

  t_left_ = peek(g_, 0);

  if (char_at(Token_get_val(t_left_), 0) == 40) # (
    consume_sym_paren_l(g_); # (
    expr_node_l_ = parse_expr(g_);
    consume_sym_paren_r(g_); # )

    return _parse_expr_right(g_, expr_node_l_);
  else

    case
    when (Token_kind_eq(t_left_, TOKEN_KIND__INT()))
      inc_token_index(g_);

      n = parse_int(Token_get_val(t_left_));
      expr_node_l_ = Node_new_int(g_, n);

      return _parse_expr_right(g_, expr_node_l_);

    when (Token_kind_eq(t_left_, TOKEN_KIND__IDENT()))
      inc_token_index(g_);

      expr_node_l_ = Node_new_str(g_, Token_get_val(t_left_));

      return _parse_expr_right(g_, expr_node_l_);

    else
      Json_print(Token_to_plain(g_, t_left_));
      panic(433); # not yet impl
    end

  end

end

def parse_set(g_)
  var stmt_;
  var [4]str_set;
  var t_;
  var var_name_;
  var expr_node_;

  str_set_set(&str_set);

  consume_kw(g_, &str_set);

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var_name_ = Token_get_val(t_);

  consume_sym_equal(g_); # =

  expr_node_ = parse_expr(g_);

  consume_sym_semicolon(g_); # ;

  stmt_ = NodeList_new(g_);

  NodeList_add_str(g_, stmt_, &str_set);

  NodeList_add_str(g_, stmt_, var_name_);

  NodeList_add(stmt_, expr_node_);

  return stmt_;
end

def parse_funcall(g_)
  var t_;
  var fn_name_;
  var funcall_;
  var args_;

  t_ = peek(g_, 0);
  inc_token_index(g_);
  fn_name_ = Token_get_val(t_);

  consume_sym_paren_l(g_); # (
  args_ = parse_args(g_);
  consume_sym_paren_r(g_); # )

  funcall_ = NodeList_new(g_);
  NodeList_add_str(g_, funcall_, fn_name_);

  NodeList_add_all(funcall_, args_);

  return funcall_;
end

def parse_call(g_)
  var stmt_;
  var [5]str_call;
  var funcall_;

  str_set_call(&str_call);

  stmt_ = NodeList_new(g_);

  consume_kw(g_, &str_call);
  NodeList_add_str(g_, stmt_, &str_call);

  funcall_ = parse_funcall(g_);
  NodeList_add_all(stmt_, funcall_);

  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def parse_call_set(g_)
  var [9]str_call_set; str_set_call_set(&str_call_set);
  var funcall_;
  var t_;
  var var_name_;
  var stmt_;

  consume_kw(g_, &str_call_set);

  t_ = peek(g_, 0);
  inc_token_index(g_);
  var_name_ = Token_get_val(t_);

  consume_sym_equal(g_); # =

  funcall_ = parse_funcall(g_);

  consume_sym_semicolon(g_);

  stmt_ = NodeList_new(g_);
  NodeList_add_str(g_, stmt_, &str_call_set);
  NodeList_add_str(g_, stmt_, var_name_);
  NodeList_add_list(g_, stmt_, funcall_);

  return stmt_;
end

def parse_return(g_)
  var expr_;
  var stmt_;
  var [7]str_return;

  str_set_return(&str_return);

  consume_kw(g_, &str_return);

  var expr_ = parse_expr(g_);

  consume_sym_semicolon(g_); # ;

  stmt_ = NodeList_new(g_);
  NodeList_add_str(g_, stmt_, &str_return);
  NodeList_add(stmt_, expr_);

  return stmt_;
end

def _parse_when_clause(g_)
  var when_clause_;
  var cond_expr_node_;
  var stmts_;

  consume_sym_paren_l(g_); # (
  cond_expr_node_ = parse_expr(g_);
  consume_sym_paren_r(g_); # )

  consume_sym_cbracket_l(g_); # {
  stmts_ = parse_stmts(g_);
  consume_sym_cbracket_r(g_); # }

  when_clause_ = NodeList_new(g_);
  NodeList_add(when_clause_, cond_expr_node_);
  NodeList_add_all(when_clause_, stmts_);

  return when_clause_;
end

def parse_case(g_)
  var stmt_;
  var [5]s_case; str_set_case(&s_case);
  var t_;
  var when_clause_;

  consume_kw(g_, &s_case);
  consume_sym_cbracket_l(g_); # {

  stmt_ = NodeList_new(g_);
  NodeList_add_str(g_, stmt_, &s_case);

  while (char_at(Token_get_val(peek(g_, 0)), 0) != 125) # }
    when_clause_ = _parse_when_clause(g_);
    NodeList_add_list(g_, stmt_, when_clause_);
  end

  consume_sym_cbracket_r(g_); # }

  return stmt_;
end

def parse_while(g_)
  var [6]s_while; str_set_while(&s_while);
  var stmt_;
  var expr_node_;
  var stmts_;

  consume_kw(g_, &s_while);

  consume_sym_paren_l(g_); # (
  expr_node_ = parse_expr(g_);
  consume_sym_paren_r(g_); # )

  consume_sym_cbracket_l(g_); # {
  stmts_ = parse_stmts(g_);
  consume_sym_cbracket_r(g_); # }

  stmt_ = NodeList_new(g_);
  NodeList_add_str(g_, stmt_, &s_while);
  NodeList_add(stmt_, expr_node_);
  NodeList_add_list(g_, stmt_, stmts_);

  return stmt_;
end

def parse_vm_comment(g_)
  var t_;
  var [5]str__cmt;
  var stmt_;

  str_set__cmt(&str__cmt);

  stmt_ = NodeList_new(g_);

  consume_kw(g_, &str__cmt);
  consume_sym_paren_l(g_); # (

  t_ = peek(g_, 0);
  inc_token_index(g_);

  NodeList_add_str(g_, stmt_, &str__cmt);
  NodeList_add_str(g_, stmt_, Token_get_val(t_));

  consume_sym_paren_r(g_); # )
  consume_sym_semicolon(g_); # ;

  return stmt_;
end

def parse_stmt(g_)
  var t_;

  t_ = peek(g_, 0);

  case
  when (str_eq_set(Token_get_val(t_)))
    return parse_set(g_);
  when (str_eq__cmt(Token_get_val(t_)))
    return parse_vm_comment(g_);
  when (str_eq_call(Token_get_val(t_)))
    return parse_call(g_);
  when (str_eq_return(Token_get_val(t_)))
    return parse_return(g_);
  when (str_eq_call_set(Token_get_val(t_)))
    return parse_call_set(g_);
  when (str_eq_while(Token_get_val(t_)))
    return parse_while(g_);
  when (str_eq_case(Token_get_val(t_)))
    return parse_case(g_);
  else
    p_str(Token_get_val(t_));
    panic(421); # parse_stmt
  end
end

def parse_stmts(g_)
  var stmt_;
  var stmts_;
  var do_break = FALSE();
  var t_;

  stmts_ = NodeList_new(g_);

  while (__not(do_break))
    t_ = peek(g_, 0);

    if (char_at(Token_get_val(t_), 0) == 125) # }
      do_break = TRUE();
    else
      stmt_ = parse_stmt(g_);
      NodeList_add_list(g_, stmts_, stmt_);
    end
  end

  return stmts_;
end

def parse_top_stmt(g_)
  var t_ = peek(g_, 0);

  if (str_eq_func(Token_get_val(t_)))
    return parse_func(g_);
  else
    panic(770); # parse_top_stmt
  end
end

def parse_top_stmts(g_)
  var top_stmts_;
  var top_stmt_;
  var [10]temp_str;

  top_stmts_ = NodeList_new(g_);

  aset(&temp_str, 0, 116); # t
  aset(&temp_str, 1, 111); # o
  aset(&temp_str, 2, 112); # p
  aset(&temp_str, 3,  95); # _
  aset(&temp_str, 4, 115); # s
  aset(&temp_str, 5, 116); # t
  aset(&temp_str, 6, 109); # m
  aset(&temp_str, 7, 116); # t
  aset(&temp_str, 8, 115); # s
  aset(&temp_str, 9,   0);
  NodeList_add_str(g_, top_stmts_, &temp_str);

  while (__not(is_end(g_)))
    top_stmt_ = parse_top_stmt(g_);
    NodeList_add_list(g_, top_stmts_, top_stmt_);
  end

  return top_stmts_;
end

def parse(g_)
  return parse_top_stmts(g_);
end

def GO_TOKENS() return GO_ALLOC_CURSOR() + GS_ALLOC_CURSOR(); end
def GS_TOKENS() return 200; end

def GO_TOKEN_INDEX() return GO_TOKENS() + GS_TOKENS(); end
def GS_TOKEN_INDEX() return 1; end

def GO_NUM_TOKENS() return GO_TOKEN_INDEX() + GS_TOKEN_INDEX(); end
def GS_NUM_TOKENS() return 1; end

def NUM_TOKENS_MAX()
  return GS_TOKENS();
end

def INPUT_MAX() return 1000; end

def main()
  var [203]g;
  var [1000]input; # INPUT_MAX
  var num_tokens;

  # init globals
  init_alloc_cursor(&g);
  set_token_index(&g, 0);
  assert(0, get_token_index(&g), 183);

  read_stdin_all(&input, INPUT_MAX());
  num_tokens = read_tokens(&g, &input);
  set_num_tokens(&g, num_tokens);

  var ast_ = parse(&g);

  Json_print(ast_);
end
